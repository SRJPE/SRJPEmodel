# This is a refactor of estN_allMR.bug
# This version allows missing MR data for some weekly strata but assumes unmarked catch for each strata

model {

  ### pCap Estimation when there is no missing MR data

  # tributary-specific mean pCap is a random effect, so define priors for hyper-paramaters (distribution of tributary mean pCap across tribs)
  pCap_mu_prior ~ dnorm(0, 1.0E-06)
  pCap_tau_prior ~ dgamma(0.001, 0.001)
  pCap_sd_prior <- 1 / pow(pCap_tau_prior, 0.5)

  flow_mu_prior ~ dnorm(0, 1.0E-06)
  flow_tau_prior ~ dgamma(0.001, 0.001)
  flow_sd_prior <- 1 / pow(flow_tau_prior, 0.5)

  for(j in 1:Ntribs) {
  	b0_pCap[j] ~ dnorm(pCap_mu_prior, pCap_tau_prior) # trib-specific P is a random draw from hyper distribtion
  	b_flow[j] ~ dnorm(flow_mu_prior, flow_tau_prior)
  }

  process_error_tau_prior ~ dgamma(0.001, 0.001) # process error distribution (error not explained by trib, year, and week effects
  process_error_sd_prior <- 1 / pow(process_error_tau_prior, 0.5)

  for(i in 1:Nmr){
    # loop through all MR experiments
  	process_error_devs_prior[i] ~ dnorm(0, process_error_tau_prior)

  	#trib and week effects only
  	logit_pCap[i] <- b0_pCap[ind_trib[i]] + b_flow[ind_trib[i]] * mr_flow[i] + process_error_devs_prior[i]
  	logit(pCap[i]) <- logit_pCap[i]
  	Recaptures[i] ~ dbin(pCap[i], Releases[i])
  }

  ### Unmarked abundance estimation ##########

  # Priors on SD terms controlling variability in spline parameters (flexibility) and extra-spline variation
  tau_N ~ dgamma(1,0.05)
  sd_N <- 1 / pow(tau_N, 0.5)
  tau_Ne ~ dgamma(1, 0.05)
  sd_Ne <- 1 / pow(tau_Ne, 0.5) # 0.1, 0.1 Prior on extra-spline variation in logU. Relative to tauN prior, tauNe has higher probs for greater precision which reduces overfitting problem (pValue too high)

  #Priors on spline coefficients
  b_sp[1] ~ dnorm(0, 1.0)
  b_sp[2] ~ dflat()
  for(i in 3:K) {
        xi[i-2] <- 2 * b_sp[i-1] - b_sp[i-2]	 #Second order random walk to set mean of normal prior on b
        b_sp[i] ~ dnorm(xi[i-2], tau_N) #The higher the estimate of tauN, the more precision in dnorm, hence less flexibility in b[i]'s, and hence a smoother the curve. Data and spline structure (knots and order) determine estimate of tauN
  }

  for(i in 1:Nstrata) {
  	Usp[i] <- inprod(ZP[i,], b_sp[]) # spline-based estimate of log U
  	lg_N[i] ~ dnorm(Usp[i], tau_Ne)I( ,lgN_max[i]) # spline + extra-spline variation estimate of log U
  	N[i] <- exp(lg_N[i]) * 1000.0
  	pCap_U[i] <- cut(pCap[ind_pCap[i]])	# assign pCap for current trib-yr-wk from large vector computed above (for all tribs, years, and weeks with MR data)
  }

  Ntot <- sum(N[])

  # Only loop through strata with unmarked catch observations
  for(i in 1:Nstrata_wc) {
  	u[Uwc_ind[i]] ~ dbin(pCap_U[Uwc_ind[i]], N[Uwc_ind[i]])
  }
}
